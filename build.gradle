buildscript {
    ext.kotlin_version = '1.4.32'
    ext.dokka_version = '1.4.30'

    repositories {
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'com.github.jonathanxd'
version '1.4.0-SNAPSHOT'

// IDE
apply plugin: 'idea'
apply plugin: 'eclipse'

// Programming Language
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'java'

// License
apply plugin: 'license'

// Shade
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/dokka' }
    maven { url "https://jitpack.io" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.googlecode.json-simple:json-simple:1.1.1"

    // Utilities
    implementation 'com.github.koresframework:Kores:4.0.1.base'

    implementation 'com.github.JonathanxD:JwIUtils:4.17.2'

    testImplementation "com.google.testing.compile:compile-testing:0.8"
    testImplementation "junit:junit:4.12"
    testImplementation files("${System.properties['java.home']}/../libs/tools.jar")
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

shadowJar {
    classifier = 'shaded'
}

tasks.dokkaGfm.configure {
    enabled = true
    outputDirectory = rootDir.toPath().resolve("docs").toFile()

    dokkaSourceSets {
        configureEach {
            includes.from(files("src/main/Module.md"))
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaGfm) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}
